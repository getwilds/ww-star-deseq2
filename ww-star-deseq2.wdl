version 1.0

struct SampleInfo {
    String name
    File r1
    File r2
    String condition
}

struct RefGenome {
    String name
    File fasta
    File gtf
}

workflow star_deseq2 {
  meta {
    author: "Taylor Firman"
    email: "tfirman@fredhutch.org"
    description: "WDL workflow for RNA-seq alignment via STAR and DESeq2 differential expression analysis"
    url: "https://github.com/getwilds/ww-star-deseq2"
    outputs: {
        star_bam: "STAR alignment output BAM files for each sample",
        star_bai: "Index files for the STAR alignment BAM files",
        star_gene_counts: "Gene count files generated by STAR for each sample",
        star_log_final: "Final log files from STAR alignment for each sample",
        star_log_progress: "Progress log files from STAR alignment for each sample",
        star_log: "Main log files from STAR alignment for each sample",
        star_sj: "Splice junction files from STAR alignment for each sample",
        rnaseqc_metrics: "Quality control metrics produced by RNA-SeQC for each sample",
        combined_counts_matrix: "Matrix file containing combined gene counts from all samples",
        sample_metadata: "Metadata file containing sample information and conditions",
        deseq2_all_results: "Complete DESeq2 differential expression results for all genes",
        deseq2_significant_results: "Filtered DESeq2 results containing only significant differentially expressed genes",
        deseq2_normalized_counts: "Normalized count values produced by DESeq2",
        deseq2_pca_plot: "Principal Component Analysis plot of samples based on expression patterns",
        deseq2_volcano_plot: "Volcano plot showing log fold change vs. statistical significance",
        deseq2_heatmap: "Heatmap of differentially expressed genes across samples"
    }
  }

  parameter_meta {
    samples: "List of sample objects, each containing name, r1/r2 fastq files, and condition information"
    reference_genome: "Optional reference genome object containing name, fasta, and gtf files (if not provided, GRCh38 will be downloaded)"
    reference_level: "Reference level for DESeq2 contrast (e.g., 'control' when comparing treatment vs. control)"
    contrast: "DESeq2 contrast string in the format 'condition,treatment,control'"
  }

  input {
    Array[SampleInfo] samples
    RefGenome? reference_genome
    String reference_level = ""
    String contrast = ""
  }

  if (!defined(reference_genome)) {
    call download_reference {}
  }

  RefGenome ref_genome_final = select_first([reference_genome, download_reference.genome])

  call build_star_index { input:
      reference_fasta = ref_genome_final.fasta,
      reference_gtf = ref_genome_final.gtf
  }

  scatter (sample in samples) {
    call star_align_two_pass { input:
        sample_data = sample,
        star_genome_tar = build_star_index.star_index_tar,
        ref_genome_name = ref_genome_final.name
    }

    call rnaseqc_cov { input:
        base_file_name = sample.name,
        bam_file = star_align_two_pass.bam,
        bam_index = star_align_two_pass.bai,
        ref_gtf = ref_genome_final.gtf
    }
  }

  call combine_count_matrices { input:
      gene_count_files = star_align_two_pass.gene_counts,
      sample_names = star_align_two_pass.name,
      sample_conditions = star_align_two_pass.condition
  }

  call run_deseq2 { input:
      counts_matrix = combine_count_matrices.counts_matrix,
      sample_metadata = combine_count_matrices.sample_metadata,
      reference_level = reference_level,
      contrast = contrast
  }

  output {
    Array[File] star_bam = star_align_two_pass.bam
    Array[File] star_bai = star_align_two_pass.bai
    Array[File] star_gene_counts = star_align_two_pass.gene_counts
    Array[File] star_log_final = star_align_two_pass.log_final
    Array[File] star_log_progress = star_align_two_pass.log_progress
    Array[File] star_log = star_align_two_pass.log
    Array[File] star_sj = star_align_two_pass.sj_out
    Array[File] rnaseqc_metrics = rnaseqc_cov.rnaseqc_metrics
    File combined_counts_matrix = combine_count_matrices.counts_matrix
    File sample_metadata = combine_count_matrices.sample_metadata
    File deseq2_all_results = run_deseq2.deseq2_results
    File deseq2_significant_results = run_deseq2.deseq2_significant
    File deseq2_normalized_counts = run_deseq2.deseq2_normalized_counts
    File deseq2_pca_plot = run_deseq2.deseq2_pca_plot
    File deseq2_volcano_plot = run_deseq2.deseq2_volcano_plot
    File deseq2_heatmap = run_deseq2.deseq2_heatmap
  }
}

task download_reference {
  meta {
    description: "Task for pulling down the GRCh38 reference fasta and gtf."
    outputs: {
        genome: "Reference genome object containing name, fasta, and gtf files for GRCh38"
    }
  }

  parameter_meta {}

  input {}

  command <<<
    set -eo pipefail
    BASE_URL="https://ftp.ncbi.nlm.nih.gov/genomes/refseq"
    ORGANISM_DIR="vertebrate_mammalian/Homo_sapiens"
    ASSEMBLY_DIR="latest_assembly_versions/GCF_000001405.40_GRCh38.p14"
    FULL_DIR="${BASE_URL}/${ORGANISM_DIR}/${ASSEMBLY_DIR}"
    wget ${FULL_DIR}/GCF_000001405.40_GRCh38.p14_genomic.fna.gz
    wget ${FULL_DIR}/GCF_000001405.40_GRCh38.p14_genomic.gtf.gz
    gunzip GCF_000001405.40_GRCh38.p14_genomic.fna.gz
    gunzip GCF_000001405.40_GRCh38.p14_genomic.gtf.gz
  >>>

  output {
    RefGenome genome = object {
      name: "hg38",
      fasta: "GCF_000001405.40_GRCh38.p14_genomic.fna",
      gtf: "GCF_000001405.40_GRCh38.p14_genomic.gtf"
    }
  }

  runtime {
    docker: "getwilds/gtf-smash:v8"
    memory: "4 GB"
    cpu: 1
  }
}

task build_star_index {
  meta {
    description: "Task for building the STAR index files from fasta/gtf."
    outputs: {
        star_index_tar: "Compressed tarball containing the STAR genome index for future alignment steps"
    }
  }

  parameter_meta {
    reference_fasta: "Reference genome FASTA file"
    reference_gtf: "Reference genome GTF annotation file"
    sjdb_overhang: "Length of the genomic sequence around the annotated junction to be used in constructing the splice junctions database"
    genome_sa_index_nbases: "Length (bases) of the SA pre-indexing string, typically between 10-15 (scales with genome size)"
    memory_gb: "Memory allocated for the task in GB"
    cpu_cores: "Number of CPU cores allocated for the task"
  }

  input {
    File reference_fasta
    File reference_gtf
    Int sjdb_overhang = 100
    Int genome_sa_index_nbases = 14
    Int memory_gb = 64
    Int cpu_cores = 8
  }

  command <<<
    set -eo pipefail
    
    mkdir star_index

    echo "Building STAR index..."
    STAR \
      --runMode genomeGenerate \
      --runThreadN ~{cpu_cores} \
      --genomeDir star_index \
      --genomeFastaFiles "~{reference_fasta}" \
      --sjdbGTFfile "~{reference_gtf}" \
      --sjdbOverhang ~{sjdb_overhang} \
      --genomeSAindexNbases ~{genome_sa_index_nbases}

    tar -czf star_index.tar.gz star_index/
  >>>

  output {
    File star_index_tar = "star_index.tar.gz"
  }

  runtime {
    docker: "getwilds/star:2.7.6a"
    memory: "~{memory_gb} GB"
    cpu: cpu_cores
  }
}

task star_align_two_pass {
  meta {
    description: "Task for aligning RNA-seq reads using STAR's two-pass technique."
    outputs: {
        name: "Sample name extracted from the input sample_data",
        condition: "Experimental condition of the sample from input sample_data",
        bam: "Aligned reads in sorted BAM format",
        bai: "Index file for the aligned BAM file",
        gene_counts: "Gene-level read counts generated by STAR",
        log_final: "Final summary log of the STAR alignment process",
        log_progress: "Progress log containing time and resource usage during alignment",
        log: "Main log file from STAR containing detailed alignment information",
        sj_out: "Splice junction file with coordinates of detected splice junctions"
    }
  }

  parameter_meta {
    star_genome_tar: "Compressed tarball containing STAR genome index"
    sample_data: "Sample information including name, input fastq files, and experimental condition"
    ref_genome_name: "Reference genome name to include in output filenames"
    sjdb_overhang: "Length of the genomic sequence around the annotated junction"
    memory_gb: "Memory allocated for the task in GB"
    cpu_cores: "Total number of CPU cores allocated for the task"
    star_threads: "Number of threads to use for STAR alignment (subset of cpu_cores)"
  }

  input {
    File star_genome_tar
    SampleInfo sample_data
    String ref_genome_name
    Int sjdb_overhang = 100
    Int memory_gb = 62
    Int cpu_cores = 8
    Int star_threads = 6
  }

  command <<<
    set -eo pipefail

    echo "Extracting STAR reference..."
    tar -xvf "~{star_genome_tar}"

    echo "Starting STAR alignment..."
    STAR \
      --genomeDir star_index \
      --readFilesIn "~{sample_data.r1}" "~{sample_data.r2}" \
      --runThreadN ~{star_threads} \
      --readFilesCommand zcat \
      --sjdbOverhang ~{sjdb_overhang} \
      --outSAMtype BAM SortedByCoordinate \
      --twopassMode Basic \
      --outTmpDir _STARtmp \
      --outFileNamePrefix "./" \
      --quantMode GeneCounts \
      --quantTranscriptomeBAMcompression 5 

    rm -r star_index _STARtmp

    mv Aligned.sortedByCoord.out.bam \
      "~{sample_data.name}.~{ref_genome_name}.Aligned.sortedByCoord.out.bam"
    mv ReadsPerGene.out.tab "~{sample_data.name}.~{ref_genome_name}.ReadsPerGene.out.tab"
    mv Log.final.out "~{sample_data.name}.~{ref_genome_name}.Log.final.out"
    mv Log.progress.out "~{sample_data.name}.~{ref_genome_name}.Log.progress.out"
    mv Log.out "~{sample_data.name}.~{ref_genome_name}.Log.out"
    mv SJ.out.tab "~{sample_data.name}.~{ref_genome_name}.SJ.out.tab"

    samtools index "~{sample_data.name}.~{ref_genome_name}.Aligned.sortedByCoord.out.bam"
  >>>

  output {
    String name = sample_data.name
    String condition = sample_data.condition
    File bam = "~{sample_data.name}.~{ref_genome_name}.Aligned.sortedByCoord.out.bam"
    File bai = "~{sample_data.name}.~{ref_genome_name}.Aligned.sortedByCoord.out.bam.bai"
    File gene_counts = "~{sample_data.name}.~{ref_genome_name}.ReadsPerGene.out.tab"
    File log_final = "~{sample_data.name}.~{ref_genome_name}.Log.final.out"
    File log_progress = "~{sample_data.name}.~{ref_genome_name}.Log.progress.out"
    File log = "~{sample_data.name}.~{ref_genome_name}.Log.out"
    File sj_out = "~{sample_data.name}.~{ref_genome_name}.SJ.out.tab"
  }

  runtime {
    docker: "getwilds/star:2.7.6a"
    memory: "~{memory_gb} GB"
    cpu: cpu_cores
  }
}

task rnaseqc_cov {
  meta {
    description: "Task for running RNA-SeQC quality control metrics on aligned RNA-seq data."
    outputs: {
        rnaseqc_metrics: "Compressed tarball containing RNA-SeQC quality metrics and coverage reports"
    }
  }

  parameter_meta {
    bam_file: "Aligned reads in BAM format"
    bam_index: "Index file for the aligned BAM file"
    ref_gtf: "Reference genome GTF annotation file (collapsed)"
    base_file_name: "Base name for output files"
    memory_gb: "Memory allocated for the task in GB"
    cpu_cores: "Number of CPU cores allocated for the task"
  }

  input {
    File bam_file
    File bam_index
    File ref_gtf
    String base_file_name
    Int memory_gb = 4
    Int cpu_cores = 2
  }

  command <<<
    set -eo pipefail

    echo "Running RNA-SeQC..."
    rnaseqc "~{ref_gtf}" "~{bam_file}" OUTPUT \
      --sample="~{base_file_name}" \
      --coverage 

    tar -cvzf "~{base_file_name}.QC.tar.gz" OUTPUT/*
  >>>

  output {
    File rnaseqc_metrics = "~{base_file_name}.QC.tar.gz"
  }

  runtime {
    docker: "getwilds/rnaseqc:2.4.2"
    memory: "~{memory_gb} GB"
    cpu: cpu_cores
  }
}

task combine_count_matrices {
  meta {
    description: "Task for combining STAR gene count files from multiple samples into a single count matrix."
    outputs: {
        counts_matrix: "Combined matrix of gene-level counts from all samples",
        sample_metadata: "Metadata file containing sample names and conditions"
    }
  }

  parameter_meta {
    gene_count_files: "Array of STAR gene count files (ReadsPerGene.out.tab) from each sample"
    sample_names: "Array of sample names corresponding to the gene_count_files"
    sample_conditions: "Array of experimental conditions corresponding to each sample"
    memory_gb: "Memory allocated for the task in GB"
    cpu_cores: "Number of CPU cores allocated for the task"
    count_column: "Column number in STAR count files to extract (2=unstranded, 3=forward strand, 4=reverse strand)"
  }

  input {
    Array[File] gene_count_files
    Array[String] sample_names
    Array[String] sample_conditions
    Int memory_gb = 4
    Int cpu_cores = 1
    Int count_column = 2
        # Column to extract from ReadsPerGene.out.tab files:
        # 2 = unstranded counts
        # 3 = stranded counts, first read forward
        # 4 = stranded counts, first read reverse
  }

  command <<<
    set -eo pipefail

    combine_star_counts.py \
      --input ~{sep=" " gene_count_files} \
      --names ~{sep=" " sample_names} \
      --conditions ~{sep=" " sample_conditions} \
      --output combined_counts_matrix.txt \
      --count_column ~{count_column}
  >>>

  output {
    File counts_matrix = "combined_counts_matrix.txt"
    File sample_metadata = "sample_metadata.txt"
  }

  runtime {
    docker: "getwilds/combine-counts:0.1.0"
    memory: "~{memory_gb} GB"
    cpu: cpu_cores
  }
}

task run_deseq2 {
  meta {
    description: "Task for analyzing differential expression via DESeq2."
    outputs: {
        deseq2_results: "Complete results of DESeq2 differential expression analysis with statistics",
        deseq2_significant: "Filtered results containing only statistically significant differentially expressed genes",
        deseq2_normalized_counts: "Normalized count values produced by DESeq2 for all samples",
        deseq2_pca_plot: "Principal Component Analysis plot showing sample clustering based on expression patterns",
        deseq2_volcano_plot: "Volcano plot showing log fold change vs. statistical significance",
        deseq2_heatmap: "Heatmap visualization of differentially expressed genes across samples"
    }
  }

  parameter_meta {
    counts_matrix: "Combined matrix of gene-level counts from all samples"
    sample_metadata: "Metadata file containing sample information including conditions"
    condition_column: "Column name in the metadata file that contains the experimental condition"
    reference_level: "Reference level for the contrast (typically the control condition)"
    contrast: "DESeq2 contrast string in the format 'condition,treatment,control'"
    memory_gb: "Memory allocated for the task in GB"
    cpu_cores: "Number of CPU cores allocated for the task"
  }

  input {
    File counts_matrix
    File sample_metadata
    String condition_column = "condition"
    String reference_level = ""
    String contrast = ""
    Int memory_gb = 8
    Int cpu_cores = 2
  }

  command <<<
    set -eo pipefail

    Rscript /deseq2_analysis.R \
      --counts_file="~{counts_matrix}" \
      --metadata_file="~{sample_metadata}" \
      --condition_column="~{condition_column}" \
      --reference_level="~{reference_level}" \
      --contrast="~{contrast}" \
      --output_prefix="deseq2_results"
  >>>

  output {
    File deseq2_results = "deseq2_results_all_genes.csv"
    File deseq2_significant = "deseq2_results_significant.csv"
    File deseq2_normalized_counts = "deseq2_results_normalized_counts.csv"
    File deseq2_pca_plot = "deseq2_results_pca.pdf"
    File deseq2_volcano_plot = "deseq2_results_volcano.pdf"
    File deseq2_heatmap = "deseq2_results_heatmap.pdf"
  }

  runtime {
    docker: "getwilds/deseq2:1.40.2"
    memory: "~{memory_gb} GB"
    cpu: cpu_cores
  }
}
